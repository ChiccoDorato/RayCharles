float clock(0.0)

# Camera
camera(perspective,
    rotationZ(30.0 - 0.25 * clock) * rotationY(0.015 * clock) *
    translation([-1.4 + 0.00175 * PI * clock, 0.0, 1.4]),
    1.33333, 1.0)

# Sky
material skyMaterial(
    diffuse(uniform(<0.0 ,0.0, 0.0>)),
    uniform(<1.0, 0.9, 0.5>)
)
sphere(skyMaterial, translation([0.0, 0.0, 0.4]) * scaling([200.0, 200.0, 200.0]))

# Ground
material groundMaterial(
    diffuse(checkered(<0.3, 0.5, 0.1>,
                      <0.1, 0.2, 0.5>,
                      10)),
    uniform(<0.0, 0.0, 0.0>)
)
plane(groundMaterial, identity)

# Table measures
float topWidth(0.86)
float topLength(1.5)
float topHeight(0.03)
float legRadius(0.027)
float legHeight(0.75)
float legIndent(0.068)

# Table top
material topMaterial(
    diffuse(uniform(<0.75, 0.6, 0.2>)),
    uniform(<0.0, 0.0, 0.0>)
)
aabox(topMaterial, translation([0.0, 0.0, legHeight]) * scaling([topWidth, topLength, topHeight]))

# Table leg
material legMaterial(
    diffuse(uniform(<0.82, 0.64, 0.1>)),
    uniform(<0.0, 0.0, 0.0>)
)
cylinder(legMaterial, translation([legIndent, legIndent, 0.0]) * scaling([legRadius, legRadius, legHeight]))
cylinder(legMaterial, translation([legIndent, topLength - legIndent, 0.0]) * scaling([legRadius, legRadius, legHeight]))
cylinder(legMaterial, translation([topWidth - legIndent, legIndent, 0.0]) * scaling([legRadius, legRadius, legHeight]))
cylinder(legMaterial, translation([topWidth - legIndent, topLength - legIndent, 0.0]) * scaling([legRadius, legRadius, legHeight]))

# Globe
float globeRadius(0.14)
material globeMaterial(
    diffuse(image("pfmImages/worldmap.pfm")),
    uniform(<0.0, 0.0, 0.0>)
)
sphere(globeMaterial,
    translation([0.0, 0.0, legHeight + topHeight + globeRadius]) *
    scaling([globeRadius, globeRadius, globeRadius])
)